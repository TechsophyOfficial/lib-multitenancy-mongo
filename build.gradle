plugins {
    id 'java'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

group = 'com.techsophy'
version = findProperty('LIBRARY_VERSION')
description = 'multitenancy-mongo'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.3.2.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb:2.3.2.RELEASE'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    //testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.7.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.3.2.RELEASE'
}

java {
    withJavadocJar()
    withSourcesJar()
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }

    }
}
check.dependsOn jacocoTestCoverageVerification


publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.techsophy'
            artifactId = 'multitenancy-mongo'
            version = '1.0.0'
            from(components.java)

            pom {
                name = 'multitenancy-mongo'
                description = 'multitenancy-mongo is an open-source Java library which has implementation of mongodb functions'
                url = 'https://github.com/TechsophyOfficial/lib-multitenancy-mongo'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'techsophyofficial'
                        name = 'Awgment owners'
                        email = 'awgment@techsophy.com'
                    }

                    scm {
                        connection = 'scm:git:git://github.com/TechsophyOfficial/lib-multitenancy-mongo.git'
                        developerConnection = 'scm:git:ssh://github.com/TechsophyOfficial/lib-multitenancy-mongo.git'
                        url = 'https://github.com/TechsophyOfficial/lib-multitenancy-mongo'
                    }
                }
            }
        }
    }


//    repositories {
//        maven {
//            name = "OSSRH"
//            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
//            credentials {
//                username = findProperty('OSSRH_USERNAME')
//                password = findProperty('OSSRH_PASSWORD')
//            }
//        }
//    }
}

signing {
    sign publishing.publications.maven
}
javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled true
    }
}
nexusPublishing {
    repositories {
        sonatype {
            //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = findProperty('OSSRH_USERNAME')
            password = findProperty('OSSRH_PASSWORD')
        }
    }
}
